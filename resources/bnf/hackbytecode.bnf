LINES = (ALU_COMMAND | MEMORY_COMMAND | BRANCHING_COMMAND | FUNCTION_COMMAND | <COMMENT_LINE> | <EOL>)+
ALU_COMMAND = ("add" | "sub" | "neg" | "eq" | "gt" | "lt" | "and" | "or" | "not") <#"\s">+? (<COMMENT> | <EOL>)
MEMORY_COMMAND = COMMAND <" "> SEGMENT <" "> i <#"\s">+? (<COMMENT> | <EOL>)
COMMAND = "push" | "pop"
SEGMENT = "local" | "argument" | "this" | "that" | "constant" | "static" | "pointer" | "temp"
i = #"\d+"
BRANCHING_COMMAND = ("label" | "goto" | "if-goto" ) <" "> LABEL <#"\s">+? (<COMMENT> | <EOL>)
LABEL = #"(\w|\.|\$)*"
FUNCTION_COMMAND = ("function" | "call" | "return") ( <" "> FUNCTION_NAME <" "> n)? (<COMMENT> | <EOL>)
FUNCTION_NAME = #"(\w|\.)*"
n = #"\d+"
<EOL>= #"\r?\n"
<COMMENT_LINE> = #"//.*"
COMMENT = (<COMMENT_LINE> <EOL>)

